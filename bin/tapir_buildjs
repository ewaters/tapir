#!/usr/bin/env perl

use strict;
use warnings;
use Getopt::Long;
use Data::Dumper;
use Thrift::IDL;
use Tapir::Validator;
use FindBin;
use List::Util qw(first);
use Tapir::Documentation::JSONSpec;

my %opt;

GetOptions(
    'idl=s' => \$opt{idl},
    'out=s' => \$opt{out},
);

if (my @missing_args = grep { ! $opt{$_} } qw(idl out)) {
    die "Must pass ".(join ', ', map { "--" . $_ } @missing_args)."\n";
}

my $document = Thrift::IDL->parse_thrift_file($opt{idl});
my $validator = Tapir::Validator->new();
my $namespace = first { defined } map { $document->headers->[0]->namespace($_) } qw(js * perl);

my @audit_errors = $validator->audit_idl_document($document);
die "IDL file failed audit\n" if @audit_errors;

my $json = Tapir::Documentation::JSONSpec->get_spec(
    namespace => $namespace,
    document => $document,
    types => $validator->{custom_types},
);

open my $out, '>', $opt{out} or die "Can't open '$opt{out}' for writing: $!";
print $out $json . "\n";
close $out;

print "Wrote JSON specification to $opt{out}\n";

